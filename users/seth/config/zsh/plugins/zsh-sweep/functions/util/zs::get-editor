#!/usr/bin/env zsh

util/zs::get-editor() {

# Run as script?
[[ $0 != */zs::get-editor ]]&&integer bescript=1

# Set options
builtin emulate -L zsh -o extendedglob -o noshortloops \
                        -o warncreateglobal -o typesetsilent \
                        -o nopromptsubst

# Possibly fix $0 with a new trick – use of a %x prompt expansion
0=${${(M)${0::=${(%):-%x}}:#/*}:-$PWD/$0}

# Input/options
local -A Opts
builtin zparseopts ${${(M)ZSH_VERSION:#(5.[8-9]|6.[0-9])}:+-F} -D -E -A Opts -- -e: || return 1

util/zs::verify-zsweep-dir||return 1

local qed1=${${${Opts[-e]:-$EDITOR}:-$VISUAL}:-mcedit}
local qed=$qed1
(($+commands[$qed]))||qed=mcedit
(($+commands[$qed]))||qed=emacs
(($+commands[$qed]))||qed=micro
(($+commands[$qed]))||qed=vim
(($+commands[$qed]))||qed=

[[ -z $qed ]]&&type -w $qed1 ZSIO&&qed=$qed1
[[ -z $qed ]]&&zsmsg {208}%BError:{137} no editor found, please set \
            {37}EDITOR{137} in environment, exiting…

REPLY=$qed
[[ -n $REPLY ]]

}
